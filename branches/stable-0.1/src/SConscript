#################################################################################
#                                                                               #
# Copyright (c) 2007 Luca Bruno                                                 #
#                                                                               #
# This file is part of Smalltalk YX.                                            #
#                                                                               #
# Permission is hereby granted, free of charge, to any person obtaining a copy  #
# of this software and associated documentation files (the "Software"), to deal #
# in the Software without restriction, including without limitation the rights  #
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell     #
# copies of the Software, and to permit persons to whom the Software is         #
# furnished to do so, subject to the following conditions:                      #
#                                                                               #
# The above copyright notice and this permission notice shall be included in    #
# all copies or substantial portions of the Software.                           #
#                                                                               #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR    #
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,      #
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE   #
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER        #
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING       #
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER           #
# DEALINGS IN THE SOFTWARE.                                                     #
#                                                                               #
#################################################################################

import os
Import ('env', 'distdir')
xenv = env.Clone ()
if env['PLATFORM'] == 'darwin':
    xenv.MergeFlags ("-bundle -flat_namespace")
xenv.MergeFlags ("-lsyx")

prog_resources = Split ("syx.ico resources.rc")
prog_sources = Split ("main.c")

prog_objects = [xenv.Object (prog_sources)]

if xenv['PLATFORM'] == 'win32' and not xenv['host']:
    prog_objects.append (xenv.RES ('resources.rc'))

prog = xenv.Program (target='syx', source=prog_objects)
Default (prog)

inst = env.SyxInstall (env['bindir'], prog)

# Create the image once installed

if not env['host']:
    image = env.Command (xenv['imagepath'], inst, '$SOURCE -r $rootdir -S')
    env.Alias ('install', image)
    env.AlwaysBuild (image)
    env.Depends (image, os.path.join(xenv['rootdir'], 'st'))

# Distribution

if env['PLATFORM'] != 'win32':
    path = os.path.join (distdir, env['rootdir'])
    bimage = env.Command (os.path.join (distdir, xenv['imagepath']), prog, '$SOURCE -r %s -i $TARGET -S' % path.replace('#', ''))
    env.Depends (bimage, os.path.join (path, 'st', 'kernel'))
    env.Alias ('bdist', bimage)

target = env.Install (os.path.join (distdir, 'src'), prog_sources + prog_resources + ['SConscript'])
env.Alias ('sdist', target)
